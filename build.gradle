import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
    id 'maven-publish'
}


version = project.minecraft_version+'-'+project.mod_version
group = project.maven_group

base {
    archivesName = archives_base_name
}

sourceSets {
    api{
        java {
            compileClasspath += main.compileClasspath
        }
    }
    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }
}

repositories {
    allprojects {
        repositories {
            maven { url 'https://jitpack.io' }
        }
    }
    maven { //AutoConfig
        name = "Shedaniel's Maven"
        url = "https://maven.shedaniel.me/"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/moreculling.accesswidener")
    runs {
        client {
            vmArg("-Dsodium.checks.issue2561=false")
            programArgs "--username", "PR0CESS", "--uuid", UUID.randomUUID().toString()
        }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Conditional-Mixins
    modImplementation "com.github.Fallen-Breath:conditional-mixin:${project.conditional_mixins_version}"
    include "com.github.Fallen-Breath:conditional-mixin:${project.conditional_mixins_version}"

    // Fabric Compat
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Sodium Compat
    modImplementation "maven.modrinth:sodium:${project.sodium_version}"
    // ^ Change to `modCompileOnly` to test without sodium

    //ModMenu Compat
    modImplementation "maven.modrinth:modmenu:${project.modmenu_version}"
    modCompileOnly "maven.modrinth:modernfix:${project.modern_fix_version}"

    //AutoConfig
    //modCompileOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixinextras_version}"))
    include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${project.mixinsquared_version}")))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(AbstractArchiveTask) {
    reproducibleFileOrder = true
    preserveFileTimestamps = false
}

tasks.withType(JavaCompile) {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

tasks.withType(Jar) {
    from "LICENSE"
    from sourceSets.api.output
}

task apiJar(type: Jar) {
    archiveClassifier = "api-dev"
    from sourceSets.api.output
}

task remapApiJar(type: RemapJarTask) {
    archiveClassifier = "api"

    input = apiJar.archiveFile.get().asFile
    addNestedDependencies = false
}
remapApiJar.dependsOn(apiJar)

// configure the maven publication
publishing {
    publications {
        main (MavenPublication) {
            artifactId "moreculling"
            artifact(remapJar)
        }

        api (MavenPublication) {
            artifactId "moreculling-api"
            artifact(remapApiJar)
        }
    }
}

build.dependsOn remapApiJar